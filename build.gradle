plugins {
    id 'org.openjfx.javafxplugin' version '0.0.8' apply false
    id 'org.jetbrains.kotlin.jvm' version '1.3.70' apply false
    id 'org.jetbrains.kotlin.kapt' version '1.3.70' apply false
}

allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
    }
}

subprojects {

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/treefrogapps/javalibs")
            credentials {
                username = project.findProperty("gpr.user")
                password = project.findProperty("gpr.public_key")
            }
        }
    }

    apply plugin: "java"
    apply plugin: "maven-publish"
    apply plugin: "org.openjfx.javafxplugin"
    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: "org.jetbrains.kotlin.kapt"

    kapt.includeCompileClasspath = false

    compileKotlin {
        kotlinOptions.jvmTarget = JavaVersion.VERSION_11
    }

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/treefrogapps/javalibs")
                credentials {
                    username = project.findProperty("gpr.user")
                    password = project.findProperty("gpr.key")
                }
            }
        }

        publications {
            gpr(MavenPublication) {
                from(components.java)
            }
        }
    }
}
